hybrid reachability
{
 state var lTPoseX, lTSpeedX, lTPoseY, lTSpeedY, 
           rTPoseX, rTSpeedX, rTPoseY, rTSpeedY,
           carPoseX, carSpeedX, carPoseY, carSpeedY, carHeading,t
          
setting 
{ 
  fixed steps 0.01
  time 6
  remainder estimation 1e-2
  identity precondition
  gnuplot octagon carPoseX, carPoseY
  fixed orders 4
  cutoff 1e-12
  precision 128
  output tintersectionModerate
  max jumps 2
  print on
}
 modes
{
  coasting
   {
     nonpoly ode 
     {
       carPoseX' = [0,0]  
       carPoseY' = [1.0,1.0]
       carSpeedX' = [0,0] 
       carSpeedY' = [0,0]
       lTPoseX' = [1.0,1.1]  
       lTPoseY' = [0,0]
       lTSpeedX' = 0  
       lTSpeedY' = [0,0]
       rTPoseX' = [-1.1,-0.9]   
       rTPoseY' = [0,0]
       rTSpeedX' = 0  
       rTSpeedY' = [0,0]
       carHeading' = [0,0]
       t' = 1
      }
     inv { carPoseY <= -1.0 }
      }
  passing
   {
     nonpoly ode 
     { 
       carPoseY' = 1.0*sin(carHeading)
       carPoseX' = 1.0*cos(carHeading) 
       carSpeedX' = 0
       carSpeedY' = 0
       carHeading' = 1  
       lTPoseX' = lTSpeedX  
       lTPoseY' = 0
       lTSpeedX' = -carPoseY - lTSpeedX  
       lTSpeedY' = 0
       rTPoseX' = -1.0   
       rTPoseY' = 0.0
       rTSpeedX' = 0.0  
       rTSpeedY' = 0.0
       t' = 1          
      }
     inv { carHeading <= 3.141592}
    }
  accelerating
   {
     nonpoly ode 
     {
       carPoseX' = carSpeedX
       carSpeedX' = 0.0
       carPoseY' = 0.0
       carSpeedY' = 0.0
       carHeading' = 0.0
       lTPoseX' = 1.0  
       lTPoseY' = 0.0
       lTSpeedX' = 0.0 
       lTSpeedY' = 0.0
       rTPoseX' = rTSpeedX   
       rTPoseY' = 0.0
       rTSpeedX' = (  carPoseX - rTPoseX ) - rTSpeedX  
       rTSpeedY' = 0
       t' = 1
      }
     inv{carPoseX <= 0.0}
    }
}
 jumps
 {
  coasting -> passing
  guard {carPoseY >= -1.0 }
  reset { carHeading' := carHeading  lTSpeedX':= 1.0}
  interval aggregation
  passing -> accelerating
  guard { carHeading >= 3.141592 }
  reset { carSpeedX' := -1.0 lTSpeedX':= lTSpeedX}
  interval aggregation
 }
 init 
 {
  coasting
  {
   carPoseX in [-0.1,0.1]
   carSpeedX in [0.0,0.0]
   carPoseY in [-3.1,-3.0]
   carSpeedY in [1.0,1.0]
   carHeading in [1.570748,1.745281] 
   lTPoseX in [-3.0, -2.9]  
   lTPoseY in [-0.3,-0.3]
   lTSpeedX in [1.0,1.1]  
   lTSpeedY in [0.0,0.0]
   rTPoseX in [3.0,3.1]  
   rTPoseY in [0.0,0.0]
   rTSpeedX in [-1.0, -0.9]  
   rTSpeedY in [0.0,0.0]
   t in [0.0,0.0]
  }
 }
}
